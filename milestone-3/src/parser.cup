import java.io.*;
import java.util.ArrayList;

parser code {:
    StringBuilder output = new StringBuilder("");
    int tempVariableCounter = 0;
	int labelCounter = 0;
    void outputWithLinebreak(String str) {
        output.append(str);
        output.append(System.getProperty("line.separator"));
    }

    void generateAssignmentStatement(Object id, Object expression) {
        outputWithLinebreak(String.format("%s = %s", id.toString(), expression.toString()));
    }

    String generateTempVariable() {
        return "t" + tempVariableCounter++;
    }

    void appendTACExpression(Object tempName, Object leftExpr, Object operator, Object rightExpr) {
        String exp = String.format("%s = %s%s%s", tempName.toString(), leftExpr.toString(), operator.toString(), rightExpr.toString());

        outputWithLinebreak(exp);
    }

    void appendFunctionBegin(Object functionName) {
        outputWithLinebreak(String.format("%s:", functionName.toString()));
        outputWithLinebreak("begin_func;");
    }

    void appendFunctionCall(Object tempName, Object name, Object args) {
        ArrayList<String> arguments = (ArrayList) args;

        // arguments to push before call
        if(!arguments.isEmpty()) {
            for(String arg: arguments) {
                outputWithLinebreak(String.format("push_params: %s", arg.toString()));
            }
        }

        // Call the function, assign to the temp var
        outputWithLinebreak(String.format("%s= call %s", tempName.toString(), name.toString()));

        // pop args if any
        if(!arguments.isEmpty()) {
            outputWithLinebreak("pop_params;");
        }
    }

    void appendShortVarThing(Object ids, Object expression) {

        ArrayList<String> idList = (ArrayList) ids;

        StringBuilder builder = new StringBuilder();

        while(!idList.isEmpty()) {
            builder.append(idList.remove(0));
            builder.append(",");
        }

        // Remove last comma like a boss !
        builder.setLength(builder.length() - 1);

        builder.append(" = ").append(expression.toString());

        outputWithLinebreak(builder.toString());
    }

    String generateOutput() {
        return output.toString();
    }
:}


	
/* Terminals (tokens returned by the scanner). */
terminal			BREAK, DEFAULT, FUNC, CASE, STRUCT, ELSE, PACKAGE, SWITCH, CONST, IF, 
					TYPE, FOR, IMPORT, RETURN, VAR, OR_OP, AND_OP, PLUS, MINUS, OPEN_PARAN, 
					CLOSE_PARAN, OPEN_CURLY, CLOSE_CURLY, OPEN_SQUARE, CLOSE_SQUARE, 
					SEMI_COLON, COMMA, DOT, COLON, COLON_EQUAL, EQUAL, CDOTS, ASTRISK, 
					SHIFT_LEFT, SHIFT_RIGHT, BAR, SLASH, EXCLAMATION, LESS_DASH, AMBERSAND,
					PERCENT, INCREMENT, DECREMENT;
terminal	String	IDENTIFIER, REL_OP, STRING_LIT, INTLIT;

/* Non-terminals */
nonterminal	sourceFile, packageClause, importDecls, importDecl, topLevelDecls,
					topLevelDecl, packageName, importSpecs, importSpec, importPath, 
					optionalSemiColon, declaration, functionDecl, methodDecl, 
					constDecl, typeDecl, varDecl, constSpecs, constSpec, identifierList, 
					expressionList, expression, binaryOp, primaryExpr, 
					unaryExpr, unaryOp, mulOp, addOp, operand, selector, index, slice, 
					arguments, literal, operandName, methodExpr, basicLit, functionLit, 
					qualifiedIdent, function, signature, functionBody, parameters, result, 
					type, parameterList, parameterDecl, block, statementList, statement, 
					typeName, typeLit, arrayType, structType, functionType, arrayLength, 
					elementType, sliceType, fieldDecls, fieldDecl, anonymousField, tag, 
					typeSpecs, typeSpec, varSpecs, varSpec, shortVarDecl, functionName, 
					methodName, receiver, receiverType, simpleStmt, returnStmt, breakStmt, 
					ifStmt, switchStmt, forStmt, expressionStmt, incDecStmt, assignment, 
					assignOp, exprSwitchStmt, initStmt, postStmt, condition, forClause, 
					exprCaseClauses, exprCaseClause, exprSwitchCase, compositeLit,
					literalType, literalValue, elementList, keyedElement, key, fieldName,
					element;


precedence left OR_OP, AND_OP, REL_OP;
precedence left PLUS, MINUS, BAR;
precedence left ASTRISK, SLASH, SHIFT_LEFT, SHIFT_RIGHT, PERCENT, AMBERSAND;
precedence left EXCLAMATION, LESS_DASH;
precedence left OPEN_PARAN, CLOSE_PARAN;
precedence left COMMA, SEMI_COLON;
precedence left COLON;

start with sourceFile;

/* The grammar */
sourceFile	::=	packageClause optionalSemiColon importDecls topLevelDecls
				{:
				    RESULT = generateOutput();
				:}
				| packageClause optionalSemiColon importDecls
				{:
				    RESULT = generateOutput();
				:};

topLevelDecls	::=	topLevelDecl:dec topLevelDecls:decs
					{:
						RESULT = dec + "\n" + decs;
					:}
					| topLevelDecl:dec
					{:
						RESULT = dec;
					:};

topLevelDecl	::=	declaration:dec
					{:
						RESULT = dec;
					:}
					| functionDecl:fdec optionalSemiColon
					{:
						RESULT = fdec;
					:}
					| methodDecl:mdec optionalSemiColon
					{:
						RESULT = mdec;
					:};

declaration	::=	constDecl:dec SEMI_COLON
				{:
					RESULT = dec;
				:}
				| varDecl:dec SEMI_COLON
				{:
					RESULT = dec;				
				:};
				
constDecl	::=	CONST constSpec:con
				{:
					RESULT = con;
				:}
				| CONST OPEN_PARAN constSpecs:cons CLOSE_PARAN
				{:
					RESULT = cons;
				:}
				| CONST OPEN_PARAN CLOSE_PARAN
				{:
					RESULT = "";
				:};


constSpecs	::=	constSpec:con SEMI_COLON constSpecs:cons
				{:
					RESULT = con + cons;
				:}
				| constSpec:con SEMI_COLON
				{:
					RESULT = con;
				:};

constSpec	::=	IDENTIFIER
				{:
				:}
				| IDENTIFIER:id EQUAL expression:exp
				{:
				    RESULT = exp;
				    generateAssignmentStatement(id, exp);
				:}
				| IDENTIFIER:id type EQUAL expression:exp
				{:
				    RESULT = exp;
				    generateAssignmentStatement(id, exp);
				:};
				
				
statement	::=	declaration
				{:
				:}
				| simpleStmt SEMI_COLON
				{:
				:}
				| returnStmt SEMI_COLON
				{:
				:}
				| breakStmt SEMI_COLON
				{:
				:}
				| block optionalSemiColon
				{:
				:}
				| ifStmt optionalSemiColon
				{:
				:}
				| switchStmt optionalSemiColon
				{:
				:}
				| forStmt optionalSemiColon
				{:
				:};


simpleStmt	::= expressionStmt
				{:
				:}
				| incDecStmt
				{:
				:}
				| assignment
				{:
				:}
				| shortVarDecl
				{:
				:};


expressionStmt	::= expression
					{:
					:};

incDecStmt	::= expression INCREMENT
				{:
				:}
				| expression DECREMENT
				{:
				:};

assignment	::=	expressionList:l1 assignOp:op expressionList:l2
				{:
				    // ArrayList<String> expList1 = (ArrayList) l1;
				    // ArrayList<String> expList2 = (ArrayList) l2;

				    // RESULT = l1.toString() + op.toString() + l2.toString();
				:};


ifStmt	::=	IF expression block
			{:
			:}
			| IF simpleStmt SEMI_COLON expression block
			{:
			:}
			| IF expression block ELSE ifStmt
			{:
			:}
			| IF expression block ELSE block
			{:
			:}
			| IF simpleStmt SEMI_COLON expression block ELSE ifStmt
			{:
			:}
			| IF simpleStmt SEMI_COLON expression block ELSE block
			{:
			:};


switchStmt	::=	exprSwitchStmt
				{:
				:};


exprSwitchStmt	::= SWITCH OPEN_CURLY exprCaseClauses CLOSE_CURLY
					{:
					:}
					| SWITCH  simpleStmt SEMI_COLON OPEN_CURLY exprCaseClauses CLOSE_CURLY
					{:
					:}
					| SWITCH expression OPEN_CURLY exprCaseClauses CLOSE_CURLY
					{:
					:}
					| SWITCH simpleStmt SEMI_COLON expression OPEN_CURLY exprCaseClauses CLOSE_CURLY
					{:
					:};


exprCaseClauses	::=	exprCaseClause exprCaseClauses
					{:
					:}
					| 
					{:
					:};

exprCaseClause	::= exprSwitchCase COLON statementList
					{:
					:};

exprSwitchCase	::=	CASE expressionList
					{:
					:}
					| DEFAULT
					{:
					:};


forStmt	::=	FOR block
			{:
			:}
			| FOR condition block
			{:
			:}
			| FOR forClause block
			{:
			:};


forClause	::=	initStmt SEMI_COLON condition SEMI_COLON postStmt
				{:
				:}
				| SEMI_COLON condition SEMI_COLON postStmt
				{:
				:}
				| initStmt SEMI_COLON SEMI_COLON postStmt
				{:
				:}
				| initStmt SEMI_COLON condition optionalSemiColon
				{:
				:}
				| SEMI_COLON SEMI_COLON postStmt
				{:
				:}
				| SEMI_COLON condition optionalSemiColon
				{:
				:}
				| initStmt SEMI_COLON optionalSemiColon
				{:
				:}
				| SEMI_COLON optionalSemiColon
				{:
				:};


condition	::=	expression
				{:
				:};

initStmt	::=	simpleStmt
				{:
				:};


postStmt	::=	simpleStmt
				{:
				:};


returnStmt	::=	RETURN
				{:
				:}
				| RETURN expressionList
				{:
				:};


breakStmt	::=	BREAK
				{:
				:};



identifierList	::=	IDENTIFIER:id
					{:
					    ArrayList<String> list = new ArrayList<String>();
					    list.add(id.toString());
					    RESULT = list;
					:}
					| IDENTIFIER:id COMMA identifierList:tail
					{:
                        ArrayList<String> list = (ArrayList) tail;
                        list.add(id.toString());
                        RESULT = list;
					:};

expressionList	::=	expression:e
					{:
					    ArrayList<String> expressionList = new ArrayList<String>();
					    expressionList.add(e.toString());
					    RESULT = expressionList;
					:}
					| expression:e COMMA expressionList:tail
					{:
                        ArrayList<String> expressionList = (ArrayList) tail;
                        expressionList.add(e.toString());
                        RESULT = expressionList;
					:};


expression	::=	unaryExpr:ue
				{:
				    RESULT = ue;
				:}
				| expression:el REL_OP:op expression:er
				{:
				    String t = generateTempVariable();
                    appendTACExpression(t, el, op, er);
                    RESULT = t;
				:}
				| expression:el OR_OP:op expression:er
				{:
				    String t = generateTempVariable();
                    appendTACExpression(t, el, op, er);
                    RESULT = t;
				:}
				| expression:el AND_OP:op expression:er
				{:
				    String t = generateTempVariable();
                    appendTACExpression(t, el, op, er);
                    RESULT = t;
				:}
				| expression:el PLUS:op expression:er
				{:
				    String t = generateTempVariable();
                    appendTACExpression(t, el, op, er);
                    RESULT = t;
				:}
				| expression:el MINUS:op expression:er
				{:
				    String t = generateTempVariable();
                    appendTACExpression(t, el, op, er);
                    RESULT = t;
				:}
				| expression:el ASTRISK:op expression:er
				{:
				    String t = generateTempVariable();
                    appendTACExpression(t, el, op, er);
                    RESULT = t;
				:}
				| expression:el SLASH:op expression:er
				{:
				    String t = generateTempVariable();
                    appendTACExpression(t, el, op, er);
                    RESULT = t;
				:}
				| expression:el PERCENT:op expression:er
				{:
				    String t = generateTempVariable();
                    appendTACExpression(t, el, op, er);
                    RESULT = t;
				:}
				| expression:el SHIFT_LEFT:op expression:er
				{:
				    String t = generateTempVariable();
                    appendTACExpression(t, el, op, er);
                    RESULT = t;
				:}
				| expression:el SHIFT_RIGHT:op expression:er
				{:
				    String t = generateTempVariable();
                    appendTACExpression(t, el, op, er);
                    RESULT = t;
				:}
				| expression:el AMBERSAND:op expression:er
				{:
				    String t = generateTempVariable();
                    appendTACExpression(t, el, op, er);
                    RESULT = t;
				:}
				| expression:el EXCLAMATION:op expression:er
				{:
				    String t = generateTempVariable();
                    appendTACExpression(t, el, op, er);
                    RESULT = t;
				:};


type	::= typeName
			{:
			:}
			| typeLit
			{:
			:};


typeName	::=	IDENTIFIER:id
				{:
				    RESULT = id;
				:};


typeLit	::=	functionType
			{:
			:}
			;


tag	::= STRING_LIT
		{:
		:};



functionType	::=	FUNC signature
					{:
					:};


varDecl	::=	VAR varSpec 
			{:
			:}
			| VAR OPEN_PARAN varSpecs CLOSE_PARAN
			{:
			:};


varSpecs	::= varSpec SEMI_COLON varSpecs
				{:
				:}
				| 
				{:
				:};

varSpec	::= IDENTIFIER:var EQUAL expression:e
			{:
			    generateAssignmentStatement(var, e);
			:}
			| IDENTIFIER type
			{:
			:}
			| IDENTIFIER:var type EQUAL expression:e
			{:
			    generateAssignmentStatement(var, e);
			:};
			


shortVarDecl	::= identifierList:ids	COLON_EQUAL	expression:e
					{:
					   // This was a pretty annoying one -_-
                       appendShortVarThing(ids, e);
					:};


functionDecl	::= FUNC functionName function
					{:
					    outputWithLinebreak("end_func;");
					:}
					| FUNC functionName signature
					{:
					    outputWithLinebreak("end_func;");
					:};

functionName	::= IDENTIFIER:fname
					{:
                        appendFunctionBegin(fname);
					:};


methodDecl	::= FUNC receiver methodName function
				{:
				:}
				| FUNC receiver methodName signature
				{:
				:};

receiver	::= parameters
				{:
				:};

		
methodName	::=	IDENTIFIER
				{:
				:};


unaryExpr	::=	primaryExpr:pexp
				{:
                    RESULT = pexp;
				:}
				| unaryOp unaryExpr:uexp
				{:
				:};

primaryExpr	::=	operand:op
				{:
				    RESULT = op;
				:}
				| primaryExpr:pexp selector:sel
				{:
				    RESULT = pexp.toString() + sel.toString();
				:}
				| primaryExpr index
				{:
				:}
				| primaryExpr slice
				{:
				:}
				| primaryExpr:pexp arguments:args
				{:
                    String tmp = generateTempVariable();

                    appendFunctionCall(tmp, pexp, args);
				:};



selector	::= DOT IDENTIFIER
				{:
				:};

index	::=	OPEN_SQUARE expression CLOSE_SQUARE
			{:
			:};

slice	::=	OPEN_SQUARE COLON CLOSE_SQUARE
			{:
			:}
			| OPEN_SQUARE expression COLON CLOSE_SQUARE
			{:
			:}
			| OPEN_SQUARE COLON expression CLOSE_SQUARE
			{:
			:}
			| OPEN_SQUARE expression COLON expression CLOSE_SQUARE
			{:
			:}
			| OPEN_SQUARE COLON expression COLON expression CLOSE_SQUARE
			{:
			:}
			| OPEN_SQUARE expression COLON expression COLON expression CLOSE_SQUARE
			{:
			:};


arguments	::=	OPEN_PARAN CLOSE_PARAN
				{:
				    RESULT = new ArrayList<String>(); // Empty args
				:}
				| OPEN_PARAN expressionList:args CLOSE_PARAN
				{:
				    RESULT = args;
				:}
				| OPEN_PARAN TYPE type COMMA expressionList CLOSE_PARAN
				{:
				:};


methodExpr	::=	DOT receiverType DOT methodName
				{:
				:};


receiverType	::=	OPEN_PARAN ASTRISK typeName CLOSE_PARAN
					{:
					:}
					| OPEN_PARAN typeName CLOSE_PARAN
					{:
					:};

operand	::=	literal:l
			{:
			    RESULT = l;
			:}
			| operandName:op_name
			{:
			    RESULT = op_name;
			:}
			| methodExpr:mexpr
			{:
			    RESULT = mexpr;
			:}
			| OPEN_PARAN expression:e CLOSE_PARAN
			{:
			    RESULT = e;
			:};

literal	::=	basicLit:l
			{:
			    RESULT = l;
			:}
			| functionLit
			{:
			:}
			| compositeLit
			{:
			:};


compositeLit	::=	literalType literalValue
					{:
					:};

literalType	::=	structType
				{:
				:}
				| arrayType
				{:
				:}
				| sliceType
				{:
				:}
				| TYPE typeName
				{:
				:}
				| OPEN_SQUARE CDOTS CLOSE_SQUARE elementType
				{:
				:};


literalValue	::=	OPEN_CURLY CLOSE_CURLY
					{:
					:}
					| OPEN_CURLY elementList CLOSE_CURLY
					{:
					:};


elementList	::=	keyedElement
				{:
				:}
				| keyedElement COMMA elementList
				{:
				:};


keyedElement	::=	element
					{:
					:}
					| key COLON element
					{:
					:};


key	::=	fieldName
		{:
		:}
		| literalValue
		{:
		:};

fieldName	::= IDENTIFIER
				{:
				:};


element	::= expression
			{:
			:}
			| literalValue
			{:
			:};

basicLit	::=	INTLIT:int_lit
				{:
				    RESULT = int_lit;
				:}
				| STRING_LIT:s
				{:
				    RESULT = s;
				:};

operandName	::=	IDENTIFIER:id
				{:
				    RESULT = id;
				:}
				| qualifiedIdent:q_id
				{:
				    RESULT = q_id;
				:};


qualifiedIdent	::=	DOT packageName DOT IDENTIFIER:id
					{:
					    RESULT = id;
					:};
					
					
functionLit	::=	FUNC function
				{:
				:};


function	::=	signature functionBody
				{:
				:};

signature	::=	parameters
				{:
				:}
				| parameters result
				{:
				:};


result	::=	parameters
			{:
			:}
			| OPEN_PARAN type CLOSE_PARAN
			{:
			:};

parameters	::=	OPEN_PARAN CLOSE_PARAN
				{:
				:}
				| OPEN_PARAN parameterList CLOSE_PARAN
				{:
				:}; 


parameterList	::=	parameterDecl
					{:
					:}
					| parameterDecl COMMA parameterList
					{:
					:};


parameterDecl	::= identifierList type
					{:
					:}
					| identifierList CDOTS type
					{:
					:};

functionBody	::= block
					{:
					:};


block	::= OPEN_CURLY statementList CLOSE_CURLY
			{:
			:};


statementList	::=	statement statementList
					{:
					:}
					| 
					{:
					:};

packageClause	::=	PACKAGE packageName
					{:
					:};


importDecls	::=	importDecl optionalSemiColon importDecls
				{:
				:}
				|
				{:
				:};

importDecl	::=	IMPORT	importSpec
				{:
				:}
				| IMPORT OPEN_PARAN importSpecs CLOSE_PARAN
				{:
				:};

importSpecs	::=	importSpec optionalSemiColon importSpecs
				{:
				:}
				| 
				{:
				:};

importSpec	::=	importPath
				{:
				:}
				| DOT importPath
				{:
				:}
				| packageName importPath
				{:
				:};
				
importPath	::=	STRING_LIT
				{:
				:};

packageName	::=	IDENTIFIER
				{:
				:};


optionalSemiColon	::=	SEMI_COLON
						{:
						:}
						| 
						{:
						:};


assignOp	::=	addOp:o EQUAL:e
				{:
				    RESULT = o.toString() + " " + e.toString();
				:} %prec EQUAL
				| mulOp:o EQUAL:e
				{:
				    RESULT = o.toString() + " " + e.toString();
				:} %prec EQUAL
				| EQUAL:e
				{:
				    RESULT = e;
				:};


binaryOp	::= OR_OP:o
				{:
				    RESULT = o;
				:}
				| AND_OP:o
				{:
				    RESULT = o;
				:}
				| REL_OP:o
				{:
				    RESULT = o;
				:}
				| addOp:o
				{:
				    RESULT = o;
				:}
				| mulOp:o
				{:
				    RESULT = o;
				:};

addOp	::=	PLUS:o
			{:
			    RESULT = o;
			:}
			| MINUS:o
			{:
			    RESULT = o;
			:}
			| BAR:o
			{:
			    RESULT = o;
			:};


mulOp	::=	ASTRISK:o
			{:
			    RESULT = o;
			:}
			| SLASH:o
			{:
			    RESULT = o;
			:}
			| SHIFT_LEFT:o
			{:
			    RESULT = o;
			:}
			| SHIFT_RIGHT:o
			{:
			    RESULT = o;
			:}
			| PERCENT:o
			{:
			    RESULT = o;
			:}
			| AMBERSAND:o
			{:
			    RESULT = o;
			:};


unaryOp	::=	PLUS:o
			{:
			    RESULT = o;
			:}
			| MINUS:o
			{:
			    RESULT = o;
			:}
			| EXCLAMATION:o
			{:
			    RESULT = o;
			:}
			| ASTRISK:o
			{:
			    RESULT = o;
			:}
			| AMBERSAND:o
			{:
			    RESULT = o;
			:}
			| LESS_DASH:o
			{:
			    RESULT = o;
			:};						
						
